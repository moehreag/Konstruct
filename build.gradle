plugins {
    id 'java'
    id "io.freefair.lombok" version "8.10.2"
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

configure(subprojects.findAll {it.name != "examples"}) {

    apply plugin: 'java'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'maven-publish'

    version = "${project.version}"
    group project.maven_group
    sourceCompatibility = JavaVersion.VERSION_16
    archivesBaseName = project.archivesBaseName

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.java.srcDirs
    }

    dependencies {

    }

    test {

    }

    jar {
        from("LICENSE.txt") {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }

    // https://stackoverflow.com/a/65830610 - Pretty cool stuff
    tasks.register('createProperties', WriteProperties) {
        File dir = new File("./src/main/resources/")
        dir.mkdirs()
        destinationFile = dir.toPath().resolve("version.properties").toFile()
        property 'version', project.version.toString()
    }

    classes {
        dependsOn createProperties
    }
    processResources {
        dependsOn createProperties
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                pom {
                    name = "konstruct ${project.name}".toString()
                    description = project.description
                    developers {
                        developer {
                            id = 'DarkKronicle'
                            url = 'https://github.com/DarkKronicle'
                        }
                    }
                }
            }
        }
    }

}